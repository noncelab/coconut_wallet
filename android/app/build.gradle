plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0.0'
}

def getFlavor() {
    def taskNames = gradle.startParameter.taskNames
    if (taskNames.any { it.toLowerCase().contains("regtest")}) {
        return "regtest"
    } else if (taskNames.any { it.toLowerCase().contains("mainnet")}) {
        return "mainnet"
    }
    throw GradleException("❌ build flavor를 추론할 수 없습니다. taskNames: ${taskNames}")
}
def flavor = getFlavor()
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key_${flavor}.properties")
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
} else {
    throw GradleException("❌ keystore properties 파일이 존재하지 않습니다: ${keystorePropertiesFile}")
}

if (flavor == "mainnet") {
    apply plugin: 'com.google.gms.google-services'
}

android {
    namespace "onl.coconut.wallet"
    compileSdk 35
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "onl.coconut.wallet"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "network"
    productFlavors {
         regtest {
            dimension "network"
            applicationIdSuffix ".regtest"
            versionNameSuffix "-regtest"
        }
        mainnet {
            dimension "network"
            applicationIdSuffix ""
            versionNameSuffix ""
        }
    }

    packagingOptions {
      jniLibs {
        useLegacyPackaging true
      }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "androidx.core:core-splashscreen:1.0.0"
}
